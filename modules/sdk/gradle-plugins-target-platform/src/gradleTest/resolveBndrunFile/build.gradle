buildscript {
	dependencies {
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins", version: "3.13.134"
	}

	repositories {
		File dir = rootProject.projectDir

		while (dir != null) {
			File tmpMavenRepositoryDir = new File(dir, ".m2-tmp")

			if (tmpMavenRepositoryDir.exists()) {
				maven {
					url tmpMavenRepositoryDir
				}
			}

			dir = dir.getParentFile();
		}

		maven {
			url "https://repository-cdn.liferay.com/nexus/content/groups/public"
		}
	}
}

apply plugin: "com.liferay.plugin"
apply plugin: "com.liferay.target.platform"

task runGradleTest

dependencies {
	compileOnly group: "com.google.guava", name: "guava", version: "23.0"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel"
	compileOnly group: "com.liferay.portal", name: "com.liferay.util.taglib"
	compileOnly group: "javax.portlet", name: "portlet-api", version: "2.0"
	compileOnly group: "javax.servlet", name: "javax.servlet-api"
	compileOnly group: "jstl", name: "jstl"
	compileOnly group: "org.osgi", name: "osgi.cmpn"

	targetPlatformBoms group: "com.liferay", name: "com.liferay.ce.portal.bom", version: "7.0.4"
	targetPlatformBoms group: "com.liferay", name: "com.liferay.ce.portal.compile.only", version: "7.0.4"

	targetPlatformBundles group: "com.google.guava", name: "guava", version: "23.0"

	targetPlatformDistro files("../../repo/distro.jar")
}

repositories {
	maven {
		url "https://repository-cdn.liferay.com/nexus/content/groups/public"
	}
}

resolve {
	bndrunFile = "test.bndrun"
}

runGradleTest {
	dependsOn resolve

	doLast {
		File bndrunFile = resolve.bndrunFile

		assert bndrunFile.text.contains("com.google.guava;version='[23.0.0,23.0.1)")
		assert bndrunFile.text.contains("com.liferay.resolve.bndrun.file;version='[1.0.0,1.0.1)")
	}
}