import com.liferay.gradle.util.copy.StripPathSegmentsAction

configurations {
	coreJSBundle
	fetch
}

task buildCoreJSBundle(type: Copy)
task buildFetch(type: Copy)
task buildIntersectionObserver(type: Copy)

buildCSS {
	imports = [
		new File(npmInstall.nodeModulesDir, "@clayui/css/src/scss")
	]
}

buildCoreJSBundle {
	eachFile new StripPathSegmentsAction(5)

	filter {
		String line ->

		line.replace "sourceMappingURL=minified.js.map", "sourceMappingURL=core-js-bundle.min.js.map"
	}

	from {
		zipTree(configurations.coreJSBundle.singleFile)
	}

	include "META-INF/resources/webjars/core-js-bundle/3.1.4/minified.js"
	include "META-INF/resources/webjars/core-js-bundle/3.1.4/minified.js.map"
	includeEmptyDirs = false
	into "classes/META-INF/resources"

	rename {
		it.replace "minified", "core-js-bundle.min"
	}
}

buildFetch {
	eachFile new StripPathSegmentsAction(5)

	from {
		zipTree(configurations.fetch.singleFile)
	}

	include "META-INF/resources/webjars/whatwg-fetch/2.0.4/fetch.js"
	includeEmptyDirs = false
	into "classes/META-INF/resources"
}

buildIntersectionObserver {
	from new File(npmInstall.nodeModulesDir, "intersection-observer")

	include "intersection-observer.js"

	into "classes/META-INF/resources"
}

classes {
	dependsOn buildCoreJSBundle
	dependsOn buildFetch
	dependsOn buildIntersectionObserver
}

dependencies {
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.impl", version: "default"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "default"
	compileOnly group: "org.apache.felix", name: "org.apache.felix.http.servlet-api", version: "1.1.2"
	compileOnly group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"
	compileOnly group: "org.osgi", name: "osgi.core", version: "6.0.0"
	compileOnly project(":apps:frontend-taglib:frontend-taglib-clay")
	compileOnly project(":apps:portal-url-builder:portal-url-builder-api")

	coreJSBundle group: "org.webjars.npm", name: "core-js-bundle", transitive: false, version: "3.1.4"

	fetch group: "org.webjars.npm", name: "whatwg-fetch", transitive: false, version: "2.0.4"
}