import com.liferay.gradle.util.OSDetector

configurations {
	forecastsBuilder

	compileOnly {
		extendsFrom forecastsBuilder
	}
}

task buildForecasts(type: JavaExec)
task formatForecasts

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

File forecastsDir = file("src/main/resources/com/liferay/commerce/initializer/customer/portal/internal/dependencies/forecasts")

buildForecasts {
	args relativePath(forecastsDir)
	classpath = sourceSets.main.output + configurations.forecastsBuilder

	doFirst {
		delete forecastsDir
	}

	finalizedBy formatForecasts
	main = "com.liferay.commerce.initializer.customer.portal.internal.tools.CustomerPortalSampleForecastsBuilder"
}

dependencies {
	compileOnly group: "com.liferay", name: "com.liferay.dynamic.data.mapping.api", version: "4.0.0-20180316.222213-1"
	compileOnly group: "com.liferay", name: "com.liferay.frontend.taglib", version: "3.0.0-20180521.175836-1"
	compileOnly group: "com.liferay", name: "com.liferay.frontend.taglib.soy", version: "1.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.portlet.display.template", version: "2.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.site.api", version: "3.0.0-20180321.162152-1"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.impl", version: "3.0.0-20180510.210925-1"
	compileOnly group: "com.liferay.portal", name: "com.liferay.util.taglib", version: "2.0.0"
	compileOnly group: "javax.portlet", name: "portlet-api", version: "3.0.0"
	compileOnly group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	compileOnly group: "org.osgi", name: "org.osgi.core", version: "5.0.0"
	compileOnly group: "org.osgi", name: "org.osgi.service.cm", version: "1.5.0"
	compileOnly group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"
	compileOnly project(":private:apps:commerce:commerce-api")
	compileOnly project(":private:apps:commerce:commerce-currency-api")
	compileOnly project(":private:apps:commerce:commerce-forecast-api")
	compileOnly project(":private:apps:commerce:commerce-organization-api")
	compileOnly project(":private:apps:commerce:commerce-product-api")

	forecastsBuilder group: "com.liferay", name: "com.liferay.petra.string", version: "2.0.0-20180530.144920-1"
	forecastsBuilder group: "com.liferay.portal", name: "com.liferay.portal.kernel", transitive: false, version: "3.0.0-20180518.171544-8"
	forecastsBuilder group: "org.json", name: "json", version: "20180130"
	forecastsBuilder project(":private:apps:commerce:commerce-forecast-api")
}

formatForecasts {
	doLast {
		forecastsDir.eachFile {
			String fileName = relativePath(it)

			logger.lifecycle "Formatting ${fileName}"

			exec {
				if (OSDetector.windows) {
					commandLine = ["cmd", "/c", "sort-json", fileName]
				}
				else {
					commandLine = ["sort-json", fileName]
				}
			}

			String json = it.getText("UTF-8")

			json = json.replace("  ", "\t")
			json = json.trim()

			it.setText json, "UTF-8"
		}
	}
}