import com.liferay.gradle.util.FileUtil

task downloadDATs

String repositoryPrivatePassword = System.getProperty("repository.private.password")
String repositoryPrivateUsername = System.getProperty("repository.private.username")

dependencies {
	provided group: "biz.aQute.bnd", name: "biz.aQute.bndlib", version: "3.1.0"
	provided group: "com.liferay", name: "com.liferay.portal.configuration.metatype", version: "2.0.0"
	provided group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "2.0.0"
	provided group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	provided group: "org.osgi", name: "org.osgi.core", version: "5.0.0"
	provided group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"
	provided project(":apps:portal-mobile-device-detection-fiftyonedegrees:portal-mobile-device-detection-fiftyonedegrees")
}

downloadDATs {
	doLast {
		File dir = new File(processResources.destinationDir, "META-INF")

		FileUtil.get(
			project,
			"https://repository.liferay.com/nexus/content/repositories/liferay-private-releases/com/liferay/com.51degrees.dat.enterprise/3.2/com.51degrees.dat.enterprise-3.2.gz",
			repositoryPrivateUsername, repositoryPrivatePassword,
			new File(dir, "51Degrees-EnterpriseV3_2.dat.gz"), false, false)

		FileUtil.get(
			project,
			"https://repository.liferay.com/nexus/content/repositories/liferay-private-releases/com/liferay/com.51degrees.dat.test/3.2/com.51degrees.dat.test-3.2.gz",
			repositoryPrivateUsername, repositoryPrivatePassword,
			new File(dir, "51Degrees-TestV3_2.dat.gz"), false, false)
	}
}

classes {
	dependsOn downloadDATs
}